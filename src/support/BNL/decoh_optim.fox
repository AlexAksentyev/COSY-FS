INCLUDE 'bin/setups/BNL';

PROCEDURE SET_RAYS BUNCH;
  VARIABLE I 1; VARIABLE J 1;
  VARIABLE X 1; VARIABLE D 1;
  VARIABLE N 1;
  CR;
  SR 1e-9 0 0 0 0 0 0 0 2; SSR 0 0 1; { serve as reference particle }
  N := 10;
  LOOP I 1 N;
    J := 2*(I - 5.5)/(N-1);
    X := J*1e-3;
    D := J*1e-4;
    IF BUNCH='X'; SR X 0 0 0 0 0 0 0 2; SSR 0 0 1;
    ELSEIF BUNCH='Y'; SR 0 0 X 0 0 0 0 0 2; SSR 0 0 1;
    ELSEIF BUNCH='D'; SR 0 0 0 0 0 D 0 0 2; SSR 0 0 1;
    ELSEIF TRUE;
      SR X 0 0 0 0 0 0 0 2; SSR 0 0 1;
      SR 0 0 X 0 0 0 0 0 2; SSR 0 0 1;
      SR 0 0 0 0 0 D 0 0 2; SSR 0 0 1;
    ENDIF;
  ENDLOOP;
ENDPROCEDURE;

FUNCTION DECOH_MEAS1 NTRN; {computes decoherence measure as}
{the MAX(STD(Sx)) [over 10 turns] after NTRN turns}
  VARIABLE SX 102; VARIABLE DUM 11; VARIABLE TRN 1;
  VARIABLE NSPI 1;
  TRR 1;
  TR NTRN -NTRN -1 -3 1.1 1.1 0 0 -12;
  NSPI := LENGTH(SPI(1));
  SX := SPI(1)|(2&NSPI);
  DUM:= STD(SX);
  LOOP TRN 1 10;
    TR 1 -1 -1 -3 1.1 1.1 0 0 -12;
    SX := SPI(1)|(2&NSPI);
    DUM := DUM&STD(SX);
  ENDLOOP;
  DECOH_MEAS1 := VMAX(DUM);
ENDFUNCTION; {DECOH_MEAS1}

PROCEDURE OPTIMIZE_SEXTUPOLES GSX GSY GSD TILTS FILEDIR MARKER ;
  VARIABLE METHOD 1; VARIABLE ERRTOL 1; VARIABLE MEASNTRN 1;
  VARIABLE OBJ 1;
  VARIABLE MU 800; VARIABLE NBAR 800 3; VARIABLE MUARR 800 4;
  VARIABLE CNT 1;
  VARIABLE PLOT 10000;
  VARIABLE X 1 100; VARIABLE Y 1 100;
  PROCEDURE OUTPIC OU BUNCH;
    VARIABLE DIM 1; VARIABLE MUARR 800 4;
    IF BUNCH='D'; DIM:=6;
    ELSEIF BUNCH='X'; DIM:=1;
    ELSEIF BUNCH='Y'; DIM:=3;
    ELSEIF TRUE; WRITE 6 '*** OPTIMIZE_SEXTUPOLES: WRONG DIMENSION';
    ENDIF;
    SET_RAYS BUNCH;
    GET_TUNE_ENSEMBLE MUARR;
    MUARR(1) := MUARR(1) - VMIN(MUARR(1));
    GRCOLR (MOD(CNT,3)+1) PLOT; CNT := CNT + 1;
    DRAW_LAYER RAY(DIM)|(3&NRAY) MUARR(1)|(3&NRAY) PLOT;
    WRITE OU PLOT;
    IF CNT>5;
      FRAME_SETUP RAY(DIM) MUARR(1) PLOT 'BUNCH '&BUNCH;
      CNT := 1;
    ENDIF;
  ENDPROCEDURE; {OUTPIC}

  {OPTIMIZATION PARAMETERS}
  ERRTOL := 1E-7; METHOD := 1; {USING SIMPLEX METHOD (LMDIF YIELDS WRONG RESULTS)}
  MEASNTRN := 49995;

  OPENF 100 FILENAME(FILEDIR, 'OPTIM_STRENGTH', MARKER) 'REPLACE';
  WRITE 100 '                GSX                GSY                GSD                OBJ';

  {OPTIMIZE sextupole strengths; objective function is decoherence measure}
  CW_SETUP 0 0 GSD TILTS;
  SET_RAYS 'D';
  GET_TUNE_ENSEMBLE MUARR;
  MUARR(1) := MUARR(1) - VMIN(MUARR(1));
  FRAME_SETUP RAY(6) MUARR(1) PLOT 'BUNCH D';
  CNT := 1;
  FIT GSD;
    CW_SETUP 0 0 GSD TILTS;
    OUTPIC -1 'D';
    TSS MU NBAR 0;
    DAPEE MU 66 OBJ; OBJ := ABS(OBJ);
    IF OBJ=0; WRITE 6 'GSD: '&ST(GSD); ENDIF;
    WRITE 6 FORM(GSX&GSY&GSD&OBJ);
    WRITE 100 FORM(GSX&GSY&GSD&OBJ);
  ENDFIT ERRTOL 1000 METHOD OBJ;
  WRITE 100 '# **************************************************';
  
  WRITE 6 'USING GSD: '&ST(GSD);
  CW_SETUP GSX 0 0 TILTS;
  SET_RAYS 'X';
  GET_TUNE_ENSEMBLE MUARR;
  MUARR(1) := MUARR(1) - VMIN(MUARR(1));
  FRAME_SETUP RAY(1) MUARR(1) PLOT 'BUNCH X';
  CNT := 1;
  FIT GSX;
    CW_SETUP GSX 0 0 TILTS;
    OUTPIC -2 'X';
    TSS MU NBAR 0;
    DAPEE MU 11 OBJ; OBJ := ABS(OBJ);
    IF OBJ=0; WRITE 6 'GSX: '&ST(GSX); ENDIF;
    WRITE 6 FORM(GSX&GSY&GSD&OBJ);
    WRITE 100 FORM(GSX&GSY&GSD&OBJ);
  ENDFIT ERRTOL 1000 METHOD OBJ;
  WRITE 100 '# **************************************************';
  
  WRITE 6 'USING GSX, GSD: '&ST(GSX)&'  '&ST(GSD);
  CW_SETUP 0 GSY 0 TILTS;
  SET_RAYS 'Y';
  GET_TUNE_ENSEMBLE MUARR;
  MUARR(1) := MUARR(1) - VMIN(MUARR(1));
  FRAME_SETUP RAY(3) MUARR(1) PLOT 'BUNCH Y';
  CNT := 1;
  FIT GSY;
    CW_SETUP 0 GSY 0 TILTS;
    OUTPIC -3 'Y';
    TSS MU NBAR 0;
    DAPEE MU 33 OBJ; OBJ := ABS(OBJ);
    IF OBJ=0; WRITE 6 'GSY: '&ST(GSY); ENDIF;
    WRITE 6 FORM(GSX&GSY&GSD&OBJ);
    WRITE 100 FORM(GSX&GSY&GSD&OBJ);
  ENDFIT ERRTOL 1000 METHOD OBJ;
  CLOSEF 100;
  
ENDPROCEDURE; {OPTIMIZE_SEXTUPOLES}

PROCEDURE GRADSWEEP DIM LOW UP NUM;
  VARIABLE DELTA 1; VARIABLE STR 1;
  VARIABLE MU 800; VARIABLE NBAR 800 3; VARIABLE MUARR 800 4;
  VARIABLE I 1; VARIABLE CNT 1;
  VARIABLE PLOT 10000;
  VARIABLE DUM 1;
  VARIABLE GSX 1; VARIABLE GSY 1; VARIABLE GSD 1;
  VARIABLE EXPARR 1 6;

  DELTA := (UP-LOW)/(NUM-1);
  IF DIM=1; STR:='X';
  ELSEIF DIM=3; STR:='Y';
  ELSEIF DIM=6; STR:='D';
  ELSEIF TRUE; WRITE 6 '*** GRADSWEEP: DIMENSION ERROR';
  ENDIF;

  IF FALSE;
    GSX := 5.85e-3; {optimal at 270 mev}
    GSY := -2.5E-3; {optimal with gsx set}
    GSD := -1.45E-2; {optimal with gsx, gsy set}
  ENDIF;
  
  CNT := 1;
  LOOP I 1 NUM;
    IF DIM=1; GSX := LOW + (I-1)*DELTA; WRITE 6 'GSX: '&ST(GSX);
    ELSEIF DIM=3; GSY := LOW + (I-1)*DELTA; WRITE 6 'GSY '&ST(GSY);
    ELSEIF TRUE; GSD := LOW + (I-1)*DELTA; WRITE 6 'GSD: '&ST(GSD);
    ENDIF;
    CW_SETUP GSX GSY GSD ZEROS(32);
    TSS MU NBAR 0;
    EXPARR(DIM):=1;
    DAPEA MU EXPARR 6 DUM; WRITE 6 'abs(POW1): '&ST(ABS(DUM)) ;
    EXPARR(DIM):=2;
    DAPEA MU EXPARR 6 DUM; WRITE 6 'abs(POW2): '&ST(ABS(DUM)) ;
    EXPARR(DIM):=3;
    DAPEA MU EXPARR 6 DUM; WRITE 6 'abs(POW3): '&ST(ABS(DUM)) '' '';
    SET_RAYS STR; GET_TUNE_ENSEMBLE MUARR; MUARR(1) := MUARR(1) - VMIN(MUARR(1));
    IF CNT=1; FRAME_SETUP RAY(DIM) MUARR(1) PLOT 'BUNCH '&STR; ENDIF;
    GRCOLR (MOD(CNT,5)+1) PLOT; CNT := CNT + 1;
    DRAW_LAYER RAY(DIM)|(3&NRAY) MUARR(1)|(3&NRAY) PLOT;
    WRITE -1 PLOT;
    IF CNT>5;
      FRAME_SETUP RAY(DIM) MUARR(1) PLOT 'BUNCH '&STR;
      CNT := 1;
    ENDIF;
  ENDLOOP;
ENDPROCEDURE; {GRADSWEEP}

SAVE 'bin/support/BNL/decoh_optim';
