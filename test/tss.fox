INCLUDE 'bin/support/BNL/decoh_optim';

PROCEDURE RUN;
  VARIABLE WHERE 100;
  VARIABLE MU 800; VARIABLE NBAR 800 3;
  VARIABLE TILTS 32; VARIABLE K 1; VARIABLE OPTCASE 100; VARIABLE TILTCASE 100;
  VARIABLE TCN 1;
  VARIABLE CASE 2 4; VARIABLE CASENUM 1; VARIABLE CASENAME 100 4;
  VARIABLE GSX 1; VARIABLE GSY 1; VARIABLE GSD 1;
  { === PROCEDURE DEFINITION === }
  PROCEDURE GEST POINT MUARR L;
    VARIABLE K 1;
    MUARR(1) := MU; LOOP K 1 3; MUARR(K+1) := NBAR(K); ENDLOOP;
    POLVAL L MUARR 4 POINT TWOND MUARR 4;
  ENDPROCEDURE;
  PROCEDURE WRITETSS; 
    OPENF 7 FILENAME(WHERE, 'MU', 'FULL') 'REPLACE'; WRITE 7 MU; CLOSEF 7;
    LOOP K 1 3;
      OPENF 7 FILENAME(WHERE, 'NBAR'&MRK(K), 'FULL') 'REPLACE';
      WRITE 7 NBAR(K); CLOSEF 7;
    ENDLOOP;
  ENDPROCEDURE;
  PROCEDURE TEST_BASE MARK; {<========== BASE TEST}
    VARIABLE PNT 1; VARIABLE STEP 1; VARIABLE TURN 1;
    VARIABLE DUMMY 800 6; {DUMMY PHASE SPACE VARIABLE}
    VARIABLE MUARR 5000 4; {HOLDS THE *VALUES* OF TSS AT DUMMY}
    VARIABLE NINI 1; VARIABLE I 1; VARIABLE Y 100;
    FUNCTION FILENAME1 NAME;
      FILENAME1 := FILENAME(WHERE, NAME, MARK);
    ENDFUNCTION;

    {BEAM GENERATION}
    NINI := 5;
    Y := LINSPACE(1e-6, 1E-3, NINI);
    CR;
    LOOP I 1 NINI; SR 0 0 Y|I 0 0 0 0 0 1; SSR 0 0 1; ENDLOOP;

    OPENF 100 FILENAME1('PRAY') 'REPLACE';
    PRAY 100; CLOSEF 100;
    OPENF 7 FILENAME1('STUNEE') 'REPLACE';
    WRITE 7 'turn pid mu0 nx ny nz';
    GEST RAY MUARR 1;
    WRITETBL 7 MUARR 4 SF(0, '(I15)');
    WRITE 6 0;
    OPENF 8 FILENAME1('TRPSPI') 'REPLACE';
    WRITE 8 'TURN PID S_X S_Y S_Z';
    WRITETBL 8 SPI 3 SF(0, '(I15)');
    OPENF 9 FILENAME1('TRPRAY') 'REPLACE';
    WRITE 9 'TURN PID X A Y B T D';
    WRITETBL 9 RAY 6 SF(0, '(I15)');


    TRR 1; STEP := 1;
    LOOP PNT 1 50; TURN := STEP*PNT;
      TR STEP -STEP -1 -3 1.1 1.1 0 0 -12;
      DUMMY(1) := RAY(1); {X}
      DUMMY(2) := RAY(2); {A}
      DUMMY(3) := (CASE(CASENUM)|1)*RAY(3); {Y}
      DUMMY(4) := (CASE(CASENUM)|2)*RAY(4); {B}
      DUMMY(5) := RAY(5); {T}
      DUMMY(6) := RAY(6); {D}
      GEST DUMMY MUARR 1;
      WRITETBL 7 MUARR 4 SF(TURN, '(I15)');
      WRITETBL 8 SPI 3 SF(TURN, '(I15)');
      WRITETBL 9 RAY 6 SF(TURN, '(I15)'); 
      WRITE 6 PNT;
    ENDLOOP;
    CLOSEF 7; CLOSEF 8; CLOSEF 9;
  ENDPROCEDURE; {TEST_BASE}
  
  PROCEDURE BEAMOFFSET LOW HIGH NUM;
    VARIABLE OFFSET NUM; VARIABLE INJECTION 1;
    PROCEDURE WRITEDECOH;
      VARIABLE I 1; VARIABLE MUARR 800 4;
      CR; LOOP I 1 NUM; SR 0 0 OFFSET|I 0 0 0 0 0 1; SSR 0 0 1; ENDLOOP;
      GEST RAY MUARR 1;
      OPENF 7 FILENAME(WHERE, 'STUNEE', 'DECOH') 'REPLACE';
      WRITETBL 7 MUARR 4 ''; CLOSEF 7;
      OPENF 7 WHERE&'PRAY:DECOH.dat' 'REPLACE';
      PRAY 7; CLOSEF 7;
      OPENF 7 WHERE&'PSPI:DECOH.dat' 'REPLACE';
      PSPI 7; CLOSEF 7;
    ENDPROCEDURE; {WRITEDECOH}
    OFFSET := LINSPACE(LOW, HIGH, NUM);
    WRITEDECOH; {OUTPUTTING DECOHERENCE DATA}
    LOOP INJECTION 1 NUM;
      WRITE 6 'INJECTION #'&MRK(INJECTION);
      TEST_BASE OFFSET|INJECTION 'CW'&MRK(INJECTION);
    ENDLOOP; {INJECTION}
  ENDPROCEDURE; {BEAMOFFSET}

  GROUTF 'img/dump/TR' 1;

  {CASES OF PHASE SPACE POINT REDUCTION}
  CASE(1) := 1&0; CASENAME(1) := 'DUMMY_NOT_B';
  CASE(2) := 0&1; CASENAME(2) := 'DUMMY_NOT_Y';
  CASE(3) := 1&1; CASENAME(3) := 'DUMMY_BOTH';
  CASE(4) := 0&0; CASENAME(4) := 'DUMMY_NEITHER';

  OV 3 3 0;
  RP 270.0092 1876.5592/AMUMEV 1;
  RPS 1 -.142987;

  DIRSET WHERE 'data/TSS/';

  LOOP TCN 1 1;
    IF TCN=1;
      TILTCASE := 'IMPERFECT';
      { TILTS := ZEROS(32); }
      { VELSET TILTS 1 5E-3/DEGRAD; VELSET TILTS 5 -2E-5/DEGRAD; }
      { VELSET TILTS 12 -2E-4/DEGRAD; VELSET TILTS 13 1.4E-6/DEGRAD; }
      { VELSET TILTS 18 5E-5/DEGRAD; VELSET TILTS 24 -1E-5/DEGRAD; }
      { VELSET TILTS 32 6E-4/DEGRAD; }
      TILTS := VEGAUSS(0, 5E-4/DEGRAD, 32, 'data/TSS/TILTS');
    ELSEIF TRUE;
      TILTCASE := 'PERFECT';
      TILTS := ZEROS(32);
    ENDIF;

    DIRSET WHERE 'data/TSS/'&TILTCASE&'/';  
    { OPTIMIZE_SEXTUPOLES GSX GSY GSD TILTS WHERE ''; } {FIND OPTIMAL SEXTUPOLE VALUES}
    { GSY := -7.5E-3; }
    
    LOOP K 1 2;
      IF K=1;
        OPTCASE:='UNOPT';
        CW_SETUP 0 0 0 TILTS;
      ELSEIF TRUE;
        OPTCASE:='OPTIM';
        CW_SETUP 0 GSY 0 TILTS;
        WRITE 6 'GSY:'&ST(GSY);
      ENDIF;
      TSS MU NBAR 0;
      DIRSET WHERE 'data/TSS/'&TILTCASE&'/'&OPTCASE&'/';  
      WRITETSS; {SAVE THE SPIN TUNE FUNCTIONS FOR THIS LATTICE CONFIGURATION}
      LOOP CASENUM 1 4;
        DIRSET WHERE 'data/TSS/'&TILTCASE&'/'&OPTCASE&'/'&CASENAME(CASENUM)&'/';
        TEST_BASE '';
      ENDLOOP; {CASENUM}
    ENDLOOP; {OPTIMIZED/UNOPTIMIZED}
  ENDLOOP; {TILT CASE}  

  WRITE 6 'OPTIMAL SEXTUPOLE GRADIENT GSY = '&ST(GSY);
ENDPROCEDURE; {RUN}

RUN; END;
