INCLUDE 'bin/support/BNL/decoh_optim';

PROCEDURE RUN;
  VARIABLE WHERE 100;
  VARIABLE GSX 1; VARIABLE GSY 1; VARIABLE GSD 1; VARIABLE TILTS 32;
  VARIABLE REVMAP 800 8; VARIABLE REVSPNR 800 3 3; VARIABLE MUARR 5000 4;
  VARIABLE TRIAL 1;
  PROCEDURE TEST_BASE TM STM MARK; {<========== BASE TEST}
    VARIABLE PNT 1; VARIABLE STEP 1; VARIABLE TURN 1;
    FUNCTION FILENAME1 NAME;
      FILENAME1 := FILENAME(WHERE, NAME, MARK);
    ENDFUNCTION;
    SNM TM MAP; S3NM STM SPNR;

    WRITE 6 'TEST CASE: '&MARK;

    OPENF 100 FILENAME1('PRAY') 'REPLACE';
    PRAY 100; CLOSEF 100;
    OPENF 7 FILENAME1('STUNEE') 'REPLACE';
    WRITE 7 'turn pid mu0 nx ny nz';
    GET_TUNE_ENSEMBLE MUARR;
    WRITETBL 7 MUARR 4 SF(0, '(I15)');
    WRITE 6 0;
    OPENF 8 FILENAME1('TRPSPI') 'REPLACE';
    WRITE 8 'TURN PID S_X S_Y S_Z';
    WRITETBL 8 SPI 3 SF(0, '(I15)');
    OPENF 9 FILENAME1('TRPRAY') 'REPLACE';
    WRITE 9 'TURN PID X A Y B T D';
    WRITETBL 9 RAY 6 SF(0, '(I15)');


    TRR 1; STEP := 20;
    LOOP PNT 1 100; TURN := STEP*PNT;
      TR STEP -STEP -1 -3 1.1 1.1 0 0 -12;
      GET_TUNE_ENSEMBLE MUARR;
      WRITETBL 7 MUARR 4 SF(TURN, '(I15)');
      WRITETBL 8 SPI 3 SF(TURN, '(I15)');
      WRITETBL 9 RAY 6 SF(TURN, '(I15)'); 
      WRITE 6 PNT;
    ENDLOOP;
    CLOSEF 7; CLOSEF 8; CLOSEF 9;
  ENDPROCEDURE; {TEST_BASE}
  PROCEDURE UTEST TM STM MARK; {<=============== UNIFORM BEAM}
    VARIABLE NINI 1; VARIABLE DELTA 1;
    VARIABLE X 1; VARIABLE I 1;

    NINI := 1000; DELTA := 1E-3/(NINI-1);

    CR; SR 1e-9 0 0 0 0 0 0 0 1; SSR 0 0 1;
    LOOP I 1 NINI; X := DELTA*2*(I - INT(NINI/2)-.5);
      SR X 0 0 0 0 0 0 0 1; SSR 0 0 1; {X}
      SR 0 0 X 0 0 0 0 0 1; SSR 0 0 1; {Y}
      SR 0 0 0 0 0 (X/10) 0 0 1; SSR 0 0 1; {D}
      SR X 0 X 0 0 0 0 0 1; SSR 0 0 1; {X, Y}
    ENDLOOP;
    
    TEST_BASE TM STM MARK;
  ENDPROCEDURE; {UTEST}
  PROCEDURE NUTEST TM STM MARK; {<=============== NON-UNIFORM BEAM}
    VARIABLE NINI 1; VARIABLE I 1;
    VARIABLE X 5000; VARIABLE Y 5000; VARIABLE D 5000;

    NINI := 4000; 
    
    X := VEGAUSS(0, 1e-3, NINI, WHERE&'SR_XGAUSS');
    Y := VEGAUSS(0, 1e-3, NINI, WHERE&'SR_YGAUSS');
    D := VEGAUSS(0, 1e-4, NINI, WHERE&'SR_YGAUSS');

    CR; SR 1e-9 0 0 0 0 0 0 0 1; SSR 0 0 1;
    LOOP I 1 NINI; SR X|I 0 Y|I 0 0 D|I 0 0 1; SSR 0 0 1; ENDLOOP;
    
    TEST_BASE TM STM MARK;
  ENDPROCEDURE; {NUTEST}

  DIRSET WHERE 'data/SMP/PRESENTATION/UNIFORM/';
  GROUTF 'img/dump/TR' 1;

  OV 5 3 0;
  RP 270.0092 1876.5592/AMUMEV 1;
  RPS 1 -.142987;

  TILTS := VEGAUSS(4e-3/DEGRAD, 5E-4/DEGRAD, 32, WHERE&'GAUSS');
  CW_SETUP GSX GSY GSD TILTS;
  MR MAP REVMAP; SMR SPNR REVSPNR;

  UTEST MAP SPNR 'CW';
  UTEST REVMAP REVSPNR 'CCW';

  IF TRUE; {MULTIPLE TRIALS TEST}
    DIRSET WHERE 'data/SMP/PRESENTATION/GAUSS/';
    LOOP TRIAL 1 30;
      NUTEST MAP SPNR 'CW'&MRK(TRIAL);
      NUTEST REVMAP REVSPNR 'CCW'&MRK(TRIAL);
     ENDLOOP;
  ENDIF;

ENDPROCEDURE; {RUN}

RUN; END;
