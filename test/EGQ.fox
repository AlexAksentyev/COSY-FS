INCLUDE 'bin/support/BNL/decoh_optim';

PROCEDURE RUN;
  VARIABLE WHERE 100;
  VARIABLE TILTS 32; VARIABLE I 1; VARIABLE J 1;
  VARIABLE X 1; VARIABLE NINI 1;
  VARIABLE GSX 1; VARIABLE GSX0 1; VARIABLE GSY 1; VARIABLE GSD 1;
  VARIABLE MUARR 25000 4; 
  VARIABLE A 100 3; VARIABLE B 100 3; VARIABLE G 100 3;
  VARIABLE R 100 3; VARIABLE F 100 6; VARIABLE MU 100 2;
  VARIABLE BX 1; VARIABLE BY 1; VARIABLE QX 1; VARIABLE QY 1; VARIABLE FTR 1;
  FUNCTION FILENAME1 NAME;
    FILENAME1 := FILENAME(WHERE, NAME, 'QUE');
  ENDFUNCTION;
  PROCEDURE TRACK;
    WRITE 6 'TRACKING';
    OPENF 8 FILENAME1('TRPRAY') 'REPLACE';
    OPENF 9 FILENAME1('TRPSPI') 'REPLACE';
    TRPRAY 8; TRPSPI 9;
    TR 10000 100 -1 -3 1.1 1.1 -1 0 -12;
    CLOSEF 8; CLOSEF 9;
  ENDPROCEDURE;

  DIRSET WHERE 'data/EGQ/';
  GROUTF 'img/dump/TR' 1;

  OV 5 2 0;
  RP 270.00 1876.5592/AMUMEV 1;
  RPS 1 -.142987;

  TILTS := ZEROS(32);
  VELSET TILTS 5 1E-1/DEGRAD;

  CW_SETUP GSX GSY GSD TILTS;
  GT MAP F MU A B G R;
  TS MU;
  BX := CONS(B(1)); QX := CONS(MU(1));
  BY := CONS(B(2)); QY := CONS(MU(2));
  FTR := SQRT(QX*BY/QY/BX); {1.317448856147563;}

  CR; NINI := 2000;
  LOOP I 1 NINI; X := 5E-3/(NINI-1)*2*(I-INT(NINI/2)-.5);
  SR  X 0 0 0 0 0 0 0 2; SSR 0 0 1;
  SR  0 0  (X*FTR) 0 0 0 0 0 2; SSR 0 0 1;
  ENDLOOP;
  GET_TUNE_ENSEMBLE MUARR;
  { WRITE 6 'MU' FORM(MUARR(1)) 'NX' FORM(MUARR(2)) 'NY' FORM(MUARR(3)) 'NZ' FORM(MUARR(4)); }
  OPENF 7 FILENAME(WHERE, 'STUNEE', 'QUE') 'REPLACE';
  WRITE 7 '# pid mu0 nx ny nz';
  WRITETBL 7 MUARR 4 ''; CLOSEF 7;

  OPENF 7 FILENAME1('PRAY') 'REPLACE'; PRAY 7; CLOSEF 7;
  OPENF 7 FILENAME1('PSPI') 'REPLACE'; PSPI 7; CLOSEF 7;
  
ENDPROCEDURE; {RUN}

RUN; END;
