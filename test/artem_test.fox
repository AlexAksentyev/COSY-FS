INCLUDE 'bin/support/BNL/decoh_optim';

PROCEDURE RUN;
  VARIABLE WHERE 100; VARIABLE SUBWHERE 100;
  VARIABLE GSX 1; VARIABLE GSY 1; VARIABLE GSD 1; VARIABLE TILTS 32;
  VARIABLE REVMAP 800 8; VARIABLE REVSPNR 800 3 3; VARIABLE MUARR 5000 4;
  VARIABLE TRIAL 1;
  PROCEDURE TEST_BASE {TM STM} MARK; {<========== BASE TEST}
    VARIABLE PNT 1; VARIABLE STEP 1; VARIABLE TURN 1;
    FUNCTION FILENAME1 NAME;
      FILENAME1 := FILENAME(WHERE, NAME, MARK);
    ENDFUNCTION;
    { SNM TM MAP; S3NM STM SPNR; }

    OPENF 100 FILENAME1('PRAY') 'REPLACE';
    PRAY 100; CLOSEF 100;
    OPENF 7 FILENAME1('STUNEE') 'REPLACE';
    WRITE 7 'turn pid mu0 nx ny nz';
    GET_TUNE_ENSEMBLE MUARR;
    WRITETBL 7 MUARR 4 SF(0, '(I15)');
    WRITE 6 0;
    OPENF 8 FILENAME1('TRPSPI') 'REPLACE';
    WRITE 8 'TURN PID S_X S_Y S_Z';
    WRITETBL 8 SPI 3 SF(0, '(I15)');
    OPENF 9 FILENAME1('TRPRAY') 'REPLACE';
    WRITE 9 'TURN PID X A Y B T D';
    WRITETBL 9 RAY 6 SF(0, '(I15)');


    TRR 1; STEP := 800; {800}
    LOOP PNT 1 1500; {1000} TURN := STEP*PNT;
      TR STEP -STEP -1 -3 1.1 1.1 0 0 -12;
      GET_TUNE_ENSEMBLE MUARR;
      WRITETBL 7 MUARR 4 SF(TURN, '(I15)');
      WRITETBL 8 SPI 3 SF(TURN, '(I15)');
      WRITETBL 9 RAY 6 SF(TURN, '(I15)'); 
      WRITE 6 PNT;
    ENDLOOP;
    { CLOSEF 7; } CLOSEF 8; CLOSEF 9;
  ENDPROCEDURE; {TEST_BASE}
  PROCEDURE UTEST {TM STM} MARK; {<=============== UNIFORM BEAM}
    VARIABLE NINI 1; VARIABLE DELTA 1;
    VARIABLE X 100; VARIABLE I 1;
    NINI := 30;

    X := LINSPACE(-1E-3, 1E-3, NINI);

    CR; { SR 0 0 0 0 0 0 0 0 1; SSR 0 0 1; }
    LOOP I 1 NINI;
      { SR X 0 0 0 0 0 0 0 1; SSR 0 0 1; }
      SR 0 0 X|I 0 0 0 0 0 1; SSR 0 0 1;
      { SR 0 0 0 0 0 (X/10) 0 0 1; SSR 0 0 1; }
    ENDLOOP;
    
    TEST_BASE {TM STM} MARK;
  ENDPROCEDURE; {UTEST}
  PROCEDURE NUTEST {TM STM} OFFSET MARK; {<=============== NON-UNIFORM BEAM}
    VARIABLE NINI 1; VARIABLE I 1;
    VARIABLE X 5000; VARIABLE Y 5000; VARIABLE D 5000;

    NINI := 30; 

    {MAKING A PLANAR Y BEAM FOR DEBUGGING PURPOSES;}
    {TESTING IF THE DATA PERTURBATIONS ARE DUE TO SPIN TUNE DECOHERENCE}
    {INSTEAD OF NBAR VARIATION}
    X := ZEROS(NINI); { VEGAUSS(0, 1e-3, NINI, WHERE&'SR_XGAUSS'); }
    Y := VEGAUSS(OFFSET, 1e-4, NINI, WHERE&'SR_YGAUSS');
    D := ZEROS(NINI); { VEGAUSS(0, 1e-4, NINI, WHERE&'SR_YGAUSS'); }

    CR; { SR 0 0 0 0 0 0 0 0 1; SSR 0 0 1; }
    LOOP I 1 NINI; SR X|I 0 Y|I 0 0 D|I 0 0 1; SSR 0 0 1; ENDLOOP;
    
    TEST_BASE {TM STM} MARK;
  ENDPROCEDURE; {NUTEST}
  
  PROCEDURE BEAMOFFSET LOW HIGH NUM;
    VARIABLE OFFSET NUM; VARIABLE INJECTION 1;
    PROCEDURE WRITEDECOH;
      VARIABLE Y NUM; VARIABLE I 1; VARIABLE MUARR 800 4;
      Y := LINSPACE(LOW, HIGH, NUM);
      CR; LOOP I 1 NUM; SR 0 0 Y|I 0 0 0 0 0 1; SSR 0 0 1; ENDLOOP;
      GET_TUNE_ENSEMBLE MUARR;
      OPENF 7 FILENAME(WHERE, 'STUNEE', 'DECOH') 'REPLACE';
      WRITETBL 7 MUARR 4 ''; CLOSEF 7;
      OPENF 7 WHERE&'PRAY:DECOH.dat' 'REPLACE';
      PRAY 7; CLOSEF 7;
      OPENF 7 WHERE&'PSPI:DECOH.dat' 'REPLACE';
      PSPI 7; CLOSEF 7;
    ENDPROCEDURE; {WRITEDECOH}
    OFFSET := LINSPACE(LOW, HIGH, NUM);
    LOOP INJECTION 1 NUM;
      WRITE 6 'INJECTION #'&MRK(INJECTION);
      NUTEST {MAP SPNR} OFFSET|INJECTION 'CW'&MRK(INJECTION);
    ENDLOOP; {INJECTION}
    WRITEDECOH; {OUTPUTTING DECOHERENCE DATA}
  ENDPROCEDURE; {BEAMOFFSET}
  
  PROCEDURE MULTISEXT;
    VARIABLE SETTING 1; VARIABLE GSY0 1; VARIABLE GSY 11;
    GSY0 := -2.5E-3; {REFERENCE VALUE FOR THE SEXTUPOLE SETTING (SHOULD BE OPTIMAL VALUE)}
    GSY := LINSPACE(GSY0-5E-3, GSY0+5E-3, 11);
    LOOP SETTING 1 11;
      DIRSET WHERE 'data/ARTEM_TEST/'&SUBWHERE&'/SEXT'&MRK(SETTING)&'/';
      CW_SETUP 0 GSY|SETTING 0 TILTS;
      OPENF 7 WHERE&'SEXTUPOLE.set' 'REPLACE';
      WRITE 7 GSY|SETTING; CLOSEF 7;
      BEAMOFFSET -1E-3 1E-3 10; {DOING THE BEAM OFFSET TEST FOR THE SINGLE SEXTUPOLE SETTING}
    ENDLOOP; {GRADIENT}
  ENDPROCEDURE; {MULTISEXT}

  SUBWHERE := 'SEXTVAR';

  DIRSET WHERE 'data/ARTEM_TEST/'&SUBWHERE&'/';
  GROUTF 'img/dump/TR' 1;

  OV 3 3 0;
  RP 270.0092 1876.5592/AMUMEV 1;
  RPS 1 -.142987;

  TILTS := VEGAUSS(0, 5E-4/DEGRAD, 32, WHERE&'GAUSS');
  {approximate gradients (@perfect structure)}
  { GSX := 5.85e-3; }
  { GSY := -2.5e-3; }
  { GSD := -1.45e-2; }

  {SEXTUPOLE OPTIMIZATION}
  DIRSET WHERE 'data/ARTEM_TEST/'&SUBWHERE&'/decoh/';
  OPTIMIZE_SEXTUPOLES GSX GSY GSD TILTS WHERE 'OPT';

  {MAIN ANALYSIS}
  MULTISEXT; {SWEEP THROUGH SEXTUPOLE SETTINGS}

  DIRSET WHERE 'data/ARTEM_TEST/'&SUBWHERE&'/SEXT0/'; {RUN AT THE OPTIMIZED SEXTUPOLE SETTING}
  CW_SETUP 0 GSY 0 TILTS;
  OPENF 7 WHERE&'SEXTUPOLE.set' 'REPLACE';
  WRITE 7 GSY; CLOSEF 7;
  BEAMOFFSET -1e-3 1e-3 10;


ENDPROCEDURE; {RUN}

RUN; END;
